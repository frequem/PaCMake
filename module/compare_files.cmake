function(pacmake_compare_files path1 path2 out_filesEqual)
	set(${out_filesEqual} TRUE PARENT_SCOPE)
	
	if(NOT IS_DIRECTORY "${path1}" OR NOT IS_DIRECTORY "${path2}")
		if(IS_DIRECTORY "${path1}" OR IS_DIRECTORY "${path2}" OR (EXISTS "${path1}" AND NOT EXISTS "${path2}") OR (NOT EXISTS "${path1}" AND EXISTS "${path2}"))
			set(${out_filesEqual} FALSE PARENT_SCOPE)
		elseif(EXISTS "${path1}" AND EXISTS "${path2}")
			execute_process(COMMAND ${CMAKE_COMMAND} -E compare_files "${path1}" "${path2}" RESULT_VARIABLE result)
			if(NOT result EQUAL 0)
				set(${out_filesEqual} FALSE PARENT_SCOPE)
			endif()
		endif()
		return()
	endif()
	
	file(GLOB entriesDirectory1 LIST_DIRECTORIES true RELATIVE "${path1}" "${path1}/*")
	list(LENGTH entriesDirectory1 nEntriesDirectory1)
	file(GLOB entriesDirectory2 LIST_DIRECTORIES true RELATIVE "${path2}" "${path2}/*")
	list(LENGTH entriesDirectory2 nEntriesDirectory2)
	if(NOT ${nEntriesDirectory1} EQUAL ${nEntriesDirectory2})
		set(${out_filesEqual} FALSE PARENT_SCOPE)
		return()
	elseif(${nEntriesDirectory1} EQUAL 0)
		return()
	endif()
	
	set(i 0)
	while(${i} LESS ${nEntriesDirectory1})
		list(GET entriesDirectory1 ${i} entryDirectory1)
		list(GET entriesDirectory2 ${i} entryDirectory2)
		
		if(NOT entryDirectory1 STREQUAL entryDirectory2) # file(GLOB) orders lexicographically as of CMake 3.6
			set(${out_filesEqual} FALSE PARENT_SCOPE)
			return()
		endif()
		
		pacmake_compare_files("${path1}/${entryDirectory1}" "${path2}/${entryDirectory2}" entriesEqual)
		if(NOT entriesEqual)
			set(${out_filesEqual} FALSE PARENT_SCOPE)
			return()
		endif()
		
		math(EXPR i "${i} + 1")
	endwhile()
endfunction(pacmake_compare_files)
